import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'go'
    templates = 'templates/custom'
    configFile = 'templates/config.yaml'
}

// Service renaming
tasks.named('generatePOSTerminalManagement') {
    project.ext.serviceName = 'PosTerminalManagementApi'
}
tasks.named('generateLegalEntityManagement') {
    project.ext.serviceName = 'LegalEntity'
}

tasks.withType(GenerateTask).configureEach {
    enablePostProcessFile.set(true)
    globalProperties.set([
            'apis'           : '',
            'models'         : '',
            'supportingFiles': 'client.go',
            'apiTests'       : 'false',
            'apiDocs'        : 'false',
            'modelDocs'      : 'false'
    ])
    additionalProperties.set([
            'serviceName': project.ext.serviceName,
    ])
}

// Deployment
project.ext.services.each { Service svc ->
    def generateTask = "generate$svc.name" 
    def deploy = tasks.register("deploy$svc.name", Copy) {
        description "Copy $svc.name files into the repo."
        dependsOn generateTask 
        outputs.upToDateWhen { false }

        from layout.buildDirectory.dir("services/$svc.id")
        include "**/*.go"
        into layout.projectDirectory.dir("repo/src/$svc.id")
    }

    tasks.named(svc.id) { dependsOn deploy }

    tasks.named(generateTask, GenerateTask) {
        packageName.set(svc.id)
    }
}

tasks.named('generatePayment', GenerateTask) {
    packageName.set('payments')
}

tasks.named('generateLegalEntityManagement', GenerateTask) {
    packageName.set('legalentity')
}

tasks.named('deployLegalEntityManagement', Copy) {
    into layout.projectDirectory.dir("repo/src/legalentity")
}

tasks.named('deployPayment', Copy) {
    into layout.projectDirectory.dir("repo/src/payments")
}

// Small services don't need a "index"
['BalanceControl', 'BinLookup', 'Recurring', 'DataProtection', 'StoredValue', 'POSTerminalManagement'].each {
    tasks.named("deploy$it", Copy) {
        exclude 'client.go'
    }
}

// Services with a RestServiceError model
['generateBalancePlatform', 'generateTransfers', 'generateManagement'].each {
    tasks.named(it, GenerateTask) {
        additionalProperties.put('hasRestServiceError', 'true')
    }
}

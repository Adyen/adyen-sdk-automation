import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'ruby'
}

List<Service> services = project.ext.services

// Service renaming
Map<String, String> serviceNaming = project.ext.serviceNamingCamel

services.<Service> each { Service svc ->
    String serviceName = serviceNaming[svc.id]

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        configFile.set("$projectDir/config.yaml")

        additionalProperties.putAll([
                'serviceName': serviceName,
        ])
        // skip generation of models, docs, tests
        globalProperties.putAll([
                'apis': '',
                'supportingFiles': ''
        ])

        // when generating Classic Payment add prefix (namespace) for service classes
        // this is necessary to avoid collision with Checkout payments_api
        if (serviceName == 'payment') {
            additionalProperties.classicPrefix = 'Classic'
        }
        // when generating Recurring add prefix (namespace) for service classes
        // this is necessary to avoid collision with Checkout recurring_api
        if (serviceName == 'recurring') {
            additionalProperties.classicPrefix = 'Legacy'
        }
    }

    // Copy services
    def deployServices = tasks.register("deploy${svc.name}Services", Sync) {
        group 'deploy'
        description "Deploy $svc.name into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }
        onlyIf { !svc.webhook }

        // delete existing services
        doFirst {
            delete layout.projectDirectory.dir("repo/lib/adyen/services/" + serviceName)
        }

        // rename and copy generated services (i.e. bin_lookup_api.rb)
        from layout.buildDirectory.dir("services/$svc.id/lib/openapi_client/api")
        include '*_api.rb'
        into layout.projectDirectory.dir("repo/lib/adyen/services/" + serviceName)

        // rename and copy generated service facades (i.e. binLookup.rb)
        from(layout.buildDirectory.dir("services/$svc.id/api")) {
            include 'api-single.rb'
            rename 'api-single.rb', serviceName + '.rb'
            into '..'
        }
    }

    tasks.named(svc.id) { dependsOn deployServices }
}

// Tests
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/checkout/payments_api.rb").exists()
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/checkout.rb").exists()
    }
}
tasks.named('binlookup') {
    doLast {
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/binLookup/bin_lookup_api.rb").exists()
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/binLookup").exists()
    }
}

// check namespace generation
tasks.named('recurring') {
    doLast {
        // classic payment has prefix
        def paymentService = file("${layout.projectDirectory}/repo/lib/adyen/services/payment.rb").text
        assert paymentService.contains("Adyen::ClassicPaymentsApi"), "payment.rb doesn't include ClassicPaymentsApi"
        assert paymentService.contains("Adyen::ClassicModificationsApi"), "payment.rb doesn't include ClassicModificationsApi"

        // legacy recurring has prefix
        def recurringService = file("${layout.projectDirectory}/repo/lib/adyen/services/recurring.rb").text
        assert recurringService.contains("Adyen::LegacyRecurringApi"), "recurring.rb doesn't include LegacyRecurringApi"
    }
}

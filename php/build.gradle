import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    removeTags = false
}

// Service renaming
def serviceMapping = project.ext.services.collectEntries { [it.id, it.name] }
serviceMapping.putAll([
        'payment'              : 'Payments',
        'posterminalmanagement': 'POSTerminalManagement'
])

tasks.withType(GenerateTask).configureEach {
    def serviceName = serviceMapping[ext.serviceId as String]
    modelPackage.set("Model\\${serviceName}")
    apiPackage.set("Service\\${serviceName}")
    additionalProperties.putAll([
            'variableNamingConvention': 'camelCase',
            'invokerPackage'          : 'Adyen',
            'packageName'             : 'Adyen'
    ])
}

// Deployment
project.ext.services.each { Service svc ->
    def deploy = tasks.register("deploy$svc.name", Copy) {
        description "Copy $svc.name files into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }

        into layout.projectDirectory.dir("repo/src/Adyen")
        def sourcePath = "services/$svc.id/lib"

        from(layout.buildDirectory.dir(sourcePath)) {
            include "Model/**/*.php"
            include "Service/**/*.php"
        }

        from(layout.buildDirectory.file("$sourcePath/ObjectSerializer.php")) {
            into "Model/" + serviceMapping[svc.id]
        }
    }

    tasks.named(svc.id) { dependsOn deploy }
}

// Tests
tasks.named('deployAcsWebhooks', Copy) {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/AcsWebhooks/Amount.php").exists()
        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/AcsWebhooks/ObjectSerializer.php").exists()
    }
}
tasks.named('deployCheckout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/Checkout/Amount.php").exists()
        assert file("${layout.projectDirectory}/repo/src/Adyen/Service/Checkout/PaymentsApi.php").exists()
    }
}
tasks.named('payment') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/Payments/Amount.php").exists()
        assert file("${layout.projectDirectory}/repo/src/Adyen/Model/Payments/ObjectSerializer.php").exists()
        assert file("${layout.projectDirectory}/repo/src/Adyen/Service/Payments/PaymentsApi.php").exists()
    }
}

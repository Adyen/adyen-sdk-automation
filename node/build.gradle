plugins {
    id 'adyen.sdk-automation-conventions'
}

import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

project.ext {
    generator = 'typescript'
}

def services = project.ext.services as List<Service>

// Service renaming
Map<String, String> serviceNaming = project.ext.serviceNamingCamel

services.each { Service svc ->
    // service name starting lowercase
    String serviceName = serviceNaming[svc.id]
    // service unmodified (starting with uppercase)
    String originalServiceName = svc.name

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        templateDir.set("$projectDir/repo/templates-v7/typescript")

        apiNameSuffix.set('Api')
        additionalProperties.putAll([
                'modelPropertyNaming'  : 'original',
                'serviceName'          : serviceName,
                'originalServiceName'  : originalServiceName,
                'enumPropertyNaming'   : 'PascalCase',
        ])

        // for webhooks apply extra config.yaml (to generate WebhookHandler)
        if (serviceId.endsWith("webhooks")) {
            configFile.set("$projectDir/config.yaml")
        }
    }

    // Copy models
    def deployModels = tasks.register("deploy${svc.name}Models", Sync) {
        group 'deploy'
        description "Deploy $svc.name models into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }

        from(layout.buildDirectory.dir("services/$svc.id/models")) {
            eachFile { fileCopyDetails ->
                def name = fileCopyDetails.name
                if (name && name.length() > 0) {
                    // copy and rename to start with lowercase
                    fileCopyDetails.name = name[0].toLowerCase() + name.substring(1)
                }
            }

            // Rename specific files
            rename { fileName ->
                if (fileName == "all.ts") {
                    return "models.ts"
                } else if (fileName == "webhookHandler.ts") {
                    // rename to specific handler (i.e. reportWebhooksHandler.ts)
                    def handlerName = "${svc.name}Handler.ts"
                    return handlerName[0].toLowerCase() + handlerName.substring(1)
                }
                return fileName
            }

            includeEmptyDirs = false
        }
        into layout.projectDirectory.dir("repo/src/typings/" + serviceName)
    }

    // copy services
    def deployServices = tasks.register("deploy${svc.name}Services", Sync) {
        group 'deploy'
        description "Deploy $svc.name into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }
        onlyIf { !svc.webhook }

        from(layout.buildDirectory.dir("services/$svc.id/apis")) {
            include '*Api.ts'
            eachFile { fileCopyDetails ->
                def name = fileCopyDetails.name
                if (name && name.length() > 0) {
                    // copy and rename to start with lowercase
                    fileCopyDetails.name = name[0].toLowerCase() + name.substring(1)
                }
            }
            includeEmptyDirs = false
        }
        into layout.projectDirectory.dir("repo/src/services/" + serviceName)

        from(layout.buildDirectory.dir("services/$svc.id")) {
            include 'index.ts'
        }
    }

    tasks.named(svc.id) { dependsOn deployModels, deployServices }
}

// Tests
tasks.named('binlookup') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/typings/binLookup/amount.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/services/binLookupApi.ts").exists()
    }
}
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/typings/checkout/models.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/checkout/objectSerializer.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/checkout/amount.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/services/checkout/paymentsApi.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/services/checkout/index.ts").exists()
    }
}
tasks.named('balanceplatform') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/typings/balancePlatform/models.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/balancePlatform/objectSerializer.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/balancePlatform/amount.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/services/balancePlatform/accountHoldersApi.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/services/balancePlatform/index.ts").exists()
    }
}
tasks.named('acswebhooks') {
    doLast {
        assert file("${layout.projectDirectory}/repo/src/typings/acsWebhooks/acsWebhooksHandler.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/acsWebhooks/objectSerializer.ts").exists()
        assert file("${layout.projectDirectory}/repo/src/typings/acsWebhooks/amount.ts").exists()
        assert !file("${layout.projectDirectory}/repo/src/services/acsWebhooks").exists()
        assert !file("${layout.projectDirectory}/repo/src/services/acsWebhooksApi.ts").exists()
    }
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

def uri = "https://github.com/Adyen/adyen-openapi.git"
def specsDir = "$projectDir/schema"
def checkoutDir = "$projectDir/schema/json/CheckoutService-v71.json"

tasks.register('specs', Exec) {
    group 'setup'
    description 'Clone OpenAPI spec (and apply local patches).'
    commandLine 'git', 'clone', '--shallow-since=2025-09-01', uri, specsDir
    outputs.dir specsDir
    onlyIf { !file(specsDir).exists() }
    doLast {
        exec {
            workingDir specsDir
            commandLine 'git', 'checkout', 'a9d350f' // same date as .NET 32.1.3 , --before="2025-09-11 15:56:38 +0200"
        }
        exec {
            workingDir specsDir
            commandLine 'git', 'checkout', 'de5cef4ad33d5b732473eb1c0ac0d0227717e21d', '--', // // same date as .NET 32.2.0 , --before="2025-10-20 23:59:38 +0200"
                        'json/LegalEntityService-v4.json', 
                        'yaml/LegalEntityService-v4.yaml'
        }
        File folder = new File("$specsDir/json");
        assert folder.exists()
        assert folder.isDirectory()
        assert folder.list().length > 0: "Folder cannot be empty after clone"
        file(folder).eachFile { specFile ->
            if (specFile.name.endsWith('.json')) {
                def json = new JsonSlurper().parseText(specFile.text)
                // Modify the 'openapi' field
                json.openapi = '3.0.0'

                json["paths"].each { Map.Entry endpoint ->
                    endpoint.value.each { Map.Entry httpMethod ->
                        // overwrite operationId
                        httpMethod.value["operationId"] = httpMethod.value["x-methodName"]
                    }
                }

                // Overwrite the file with updated content
                specFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(json))
            }
        }
    }
}

tasks.register('pmTable', Task) {
    group 'specs'
    description 'Generate Checkout Payment Method Table'
    dependsOn('specs')
    onlyIf { file(checkoutDir).exists() }
    File folder = new File(checkoutDir);
    doLast {
        def json = new JsonSlurper().parseText(file(folder).text)
        def base = json.components.schemas
        def pmList = new HashMap<String, ArrayList<String>>()

        // find list of PaymentMethod Classes
        base.'PaymentRequest'.'properties'.'paymentMethod'.'oneOf'.each {
            pmClass -> pmList.put(pmClass.'$ref'.replace('#/components/schemas/', ''), new ArrayList())
        }
        // populate available tx variants per PaymentMethodDetailsClass
        pmList.each {
            entry ->
                def keyString = entry.getKey()
                base."$keyString".'properties'.'type'.'enum'.each {
                    pm -> entry.getValue().add(pm)
                }
        }
        outputs.println(pmList)

        // Output the list into a .md file
        def pmFile = new File("$projectDir/PaymentMethodOverview.md")
        new FileWriter(pmFile).with {
            write("# PaymentMethod Overview For Checkout\n" +
                    "| Java Class    | tx_variants   |\n" +
                    "|---------------|---------------|\n")
            pmList.each {
                entry ->
                    write("|$entry.key|$entry.value|\n")
            }
            flush()
        }
        pmFile.createNewFile()
    }
}

tasks.register('cleanSpecs', Delete) {
    group 'clean'
    description 'Delete OpenAPI specifications'
    delete specsDir
}
